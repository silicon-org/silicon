include_guard()

function(add_silicon_dialect dialect dialect_namespace)
  set(LLVM_TARGET_DEFINITIONS ${dialect}.td)
  mlir_tablegen(Dialect.cpp.inc -gen-dialect-defs -dialect=${dialect_namespace})
  mlir_tablegen(Dialect.h.inc -gen-dialect-decls -dialect=${dialect_namespace})
  mlir_tablegen(Enums.cpp.inc -gen-enum-defs)
  mlir_tablegen(Enums.h.inc -gen-enum-decls)
  mlir_tablegen(Interfaces.cpp.inc -gen-op-interface-defs)
  mlir_tablegen(Interfaces.h.inc -gen-op-interface-decls)
  mlir_tablegen(Ops.cpp.inc -gen-op-defs)
  mlir_tablegen(Ops.h.inc -gen-op-decls)
  mlir_tablegen(Passes.h.inc -gen-pass-decls)
  mlir_tablegen(Types.cpp.inc -gen-typedef-defs -typedefs-dialect=${dialect_namespace})
  mlir_tablegen(Types.h.inc -gen-typedef-decls -typedefs-dialect=${dialect_namespace})
  mlir_tablegen(Attributes.cpp.inc -gen-attrdef-defs -typedefs-dialect=${dialect_namespace})
  mlir_tablegen(Attributes.h.inc -gen-attrdef-decls -typedefs-dialect=${dialect_namespace})
  add_public_tablegen_target(Silicon${dialect}IncGen)
  add_dependencies(silicon-headers Silicon${dialect}IncGen)
endfunction()

function(add_silicon_tool name)
  add_llvm_executable(${ARGV})
  add_dependencies(silicon-tools ${name})
endfunction()

function(add_silicon_library name)
  add_mlir_library(${ARGV})
  add_dependencies(silicon-libraries ${name})
endfunction()

function(add_silicon_dialect_library name)
  set_property(GLOBAL APPEND PROPERTY SILICON_DIALECT_LIBS ${name})
  add_silicon_library(${ARGV} DEPENDS silicon-headers)
endfunction()
